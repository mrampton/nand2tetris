// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // Preset x; options are to zero x, and negate x
    Mux16(a=x, b=false, sel=zx, out=muxx);
    Not16(in=muxx, out=notx);
    Mux16(a=muxx, b=notx, sel=nx, out=xpreset);

    // Preset y; options are to zero y, and negate y
    Mux16(a=y, b=false, sel=zy, out=muxy);
    Not16(in=muxy, out=noty);
    Mux16(a=muxy, b=noty, sel=ny, out=ypreset);
    
    // Perform either Addition or And
    Add16(a=xpreset, b=ypreset, out=addxy);
    And16(a=xpreset, b=ypreset, out=andxy);
    Mux16(a=andxy, b=addxy, sel=f, out=wiref);
    
    // Pipe result to out and pass on to determine zero and negative flags
    Not16(in=wiref, out=notwiref);
    Mux16(a=wiref, b=notwiref, sel=no, out=out, out[0..7]=lowout, out[8..15]=highout, out[15]=ng);
    
    Or8Way(in=lowout, out=zero1);
    Or8Way(in=highout, out=zero2);
    Or(a=zero1, b=zero2, out=zero3);
    Not(in=zero3, out=zr);
}